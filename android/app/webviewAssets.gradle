import org.apache.tools.ant.taskdefs.condition.Os
import java.nio.file.Paths

/**
 * Build and merge static webview assets.
 *
 * This is largely the job of `$ROOT/tools/build-webview`, a script external to
 * Gradle, as there is significant overlap between the iOS and Android build
 * processes. See comments therein (or $ROOT/src/webview/static/README.md) for
 * more information.
 */

/*
 * Windows support functions.
 *
 * On Windows, we have three different possible setups:
 *   1. Windows Gradle + Git Bash.
 *   2. Windows Gradle + WSL's bash.
 *   3. WSL Gradle + WSL's bash.
 *
 * Case 3, fortunately, should behave exactly like Linux.
 *
 * In cases 1 and 2, however, all paths Gradle provides are backslash-separated.
 * This is problematic on its own, as the script we're invoking doesn't grok
 * backslashes, so we normalize those on Windows. That's enough for case 1.
 *
 * Case 2 is worse, though. The `bash` of Git Bash groks both "C:/" and "C:\";
 * more generally, it operates on the Windows filesystem with Windows path
 * names. The WSL `bash` _doesn't_ -- it's a standard Linux `bash` (or close
 * enough), and it only sees the Windows disks as ordinary mounts under the lone
 * root path '/'. Paths beginning with either "C:/" or "C:\" will confuse it
 * terribly.
 *
 * And, of course, Gradle has no good way of distinguishing between cases 1 and
 * 2. As far as it's concerned, `bash.exe` is `bash.exe`.
 *
 * Since we can't predict the environment of our callee, we can't use absolute
 * paths. Fortunately, with only a little extra work within the script itself,
 * we _can_ use relative paths; the script must derelativize them appropriately.
 */

/** Normalize the path-separators of `path` to be forward slashes. */
def normalizePath(String path) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return path.replace('\\', '/')
    } else {
        return path
    }
}

/** Return `path`, made relative to `basePath`. */
def relativizePath(String path, String basePath) {
    return Paths.get(basePath).relativize(Paths.get(path)).toString();
}

gradle.projectsEvaluated {
    // The root of our git repository.
    def repoDir = project.file('../..').absolutePath

    android.applicationVariants.all { variant ->
        // Accurate as of Android Gradle Plugin 3.4.0. Known to move between
        // releases; but alas, no stable API is known to the Internet at large.
        //
        // This should be the same as `project.ext.react.jsBundleDirRelease`
        // for release builds. See commits 8f8a266af and 833d083a9 for examples
        // of what went wrong when it changed out from under us.
        def assetsDir = "${buildDir}/intermediates/merged_assets/${variant.name}/out"

        // See above note on Windows compatibility.
        def destDir = normalizePath(relativizePath("${assetsDir}/webview", repoDir))

        def variantTask = tasks.create(
            name: "build${variant.name.capitalize()}StaticWebviewAssets",
            type: Exec
        ) {
            // All arguments to our script must be relative to `workingDir`.
            workingDir repoDir
            executable "bash"
            args "./tools/build-webview",
                "android", "--destination", destDir
        }

        // Run this task as part of assets merging for this variant.
        //
        // Caveat lector: this block is almost 100% voodoo -- it seems to work,
        // but the present author has no documentation proving it.
        variant.mergeAssetsProvider.configure {
            dependsOn variantTask
        }
    }
}
